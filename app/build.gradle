plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.kapt'
    id 'kotlin-parcelize'
}

android {
    namespace 'ir.part.app.intelligentassistant'
    compileSdk 33

    defaultConfig {
        applicationId "ir.part.app.intelligentassistant"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            File props = file(".signing-debug/signing.properties")
            File keystore = file(".signing-debug/debug.jks")
            if (props.exists()) {
                def signing = new Properties()
                props.withInputStream { signing.load(it) }
                if (signing.KEYSTORE_PASSWORD)
                    storePassword signing.getProperty('KEYSTORE_PASSWORD')
                if (signing.KEY_PASSWORD)
                    keyPassword signing.getProperty('KEY_PASSWORD')
                if (signing.KEY_ALIAS)
                    keyAlias signing.getProperty('KEY_ALIAS')
            }
            if (keystore.exists())
                storeFile keystore
        }
        release {
            File props = file(".signing/signing.properties")
            File keystore = file(".signing/release.jks")
            if (props.exists()) {
                def signing = new Properties()
                props.withInputStream { signing.load(it) }
                if (signing.KEYSTORE_PASSWORD)
                    storePassword signing.getProperty('KEYSTORE_PASSWORD')
                if (signing.KEY_PASSWORD)
                    keyPassword signing.getProperty('KEY_PASSWORD')
                if (signing.KEY_ALIAS)
                    keyAlias signing.getProperty('KEY_ALIAS')
            }
            if (keystore.exists())
                storeFile keystore
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        freeCompilerArgs += "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
        freeCompilerArgs += "-opt-in=com.google.accompanist.navigation.material.ExperimentalMaterialNavigationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation "androidx.core:core-splashscreen:1.0.1"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.lifecycle:lifecycle-process:2.6.1"

    // compose
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.compose.ui:ui:1.5.0-beta01'
    implementation 'androidx.compose.ui:ui-graphics:1.5.0-beta01'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.5.0-beta01'
    implementation "androidx.compose.foundation:foundation:1.5.0-beta01"
    implementation 'androidx.compose.material:material:1.5.0-alpha03'
    implementation "androidx.compose.runtime:runtime:1.5.0-beta02"
    debugImplementation 'androidx.compose.ui:ui-tooling:1.5.0-beta01'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.5.0-beta01'
    implementation "androidx.compose.compiler:compiler:1.4.7"

    // accompanist
    implementation "com.google.accompanist:accompanist-navigation-material:0.31.1-alpha"
    implementation "com.google.accompanist:accompanist-permissions:0.31.1-alpha"


    // pager and indicators -accompanist
    implementation "com.google.accompanist:accompanist-pager:0.31.1-alpha"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.31.1-alpha"

    //dataStore Preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // hilt
    implementation "com.google.dagger:hilt-android:2.46.1"
    kapt "com.google.dagger:hilt-android-compiler:2.46.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.2"

    // Room
    implementation 'androidx.room:room-runtime:2.5.2'
    kapt 'androidx.room:room-compiler:2.5.2'
    implementation 'androidx.room:room-ktx:2.5.2'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'

    // Moshi Kotlin
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.14.0' // TODO: use ksp
    implementation 'com.squareup.moshi:moshi:1.14.0'

    // Util
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Date
    implementation 'com.github.samanzamani:PersianDate:1.5.0'

    //iText for pdf
    implementation 'com.itextpdf:itextpdf:5.5.13.1'
    implementation "com.airbnb.android:lottie-compose:4.0.0"

    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.27'

}